# Maintainer: ≈Åukasz Fuszara <lukaszfuszara1990@gmail.com>

_realname=mono
pkgbase="mingw-w64-${_realname}"
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
_pkgver=4.8.0
pkgver=${_pkgver}.495
pkgrel=1
pkgdesc="Free implementation of the .NET platform including runtime and compiler"
arch=('any')
license=('GPL' 'LGPL2.1' 'MPL')
url="http://www.mono-project.com/"
depends=("${MINGW_PACKAGE_PREFIX}-zlib" 
         "${MINGW_PACKAGE_PREFIX}-python2" 
         "${MINGW_PACKAGE_PREFIX}-ca-certificates" 
         "${MINGW_PACKAGE_PREFIX}-gcc-libs" 
         "${MINGW_PACKAGE_PREFIX}-libiconv")
source=(http://download.mono-project.com/sources/mono/${_realname}-${pkgver}.tar.bz2
        https://raw.githubusercontent.com/lukaszfuszara/MINGW-packages/master/mingw-w64-mono/mono-4.8.0-msys2.patch)
sha256sums=('7ba62e6f42559d58dc447a19fc1cb2a9c7977d6c6e21e1e335f73917dea120cb'
            '4f24792a79a96da5c26d23257f5ecff84fe04ddcb39ed09cc8552ef8a0e2f96c')

prepare() {
 local PREFIX_WIN=$(cygpath -wm ${MINGW_PREFIX})

 cd "${srcdir}/${_realname}-${_pkgver}"
 patch -Np1 -i "${srcdir}/mono-4.8.0-msys2.patch"
}

build() {
  local PREFIX_WIN=$(cygpath -wm ${MINGW_PREFIX})

  cd "${srcdir}/${_realname}-${_pkgver}"
  
  # build mono
  ./configure \
    --prefix=${MINGW_PREFIX} \
    --target=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --enable-parallel-mark \
    --program-transform-name="" \
    --disable-boehm \
    --with-moonlight=no
  
  make get-monolite-latest
  
  mkdir -p ./mono/mini/lib/mono/4.5/
  cp ./mcs/class/lib/monolite/* ./mono/mini/lib/mono/4.5/
  
  make
}

package() {
  local PREFIX_WIN=$(cygpath -wm ${MINGW_PREFIX})
  
  cd "${srcdir}"/${_realname}-${_pkgver}
  make DESTDIR="${pkgdir}" install
}
